// ========================================================================
// 发布 *.jar 产物到本地的 maven 仓库.
// ========================================================================

apply plugin: "maven-publish"
apply plugin: "signing"


/**
 * 模块名.
 */
static String getProductArtifactId() {
    return "holder"
}

/**
 * 版本号。
 */
static String getProductVersion() {
    return "0.0.7"
}

// 配置签名，上传到 central maven 必须要签名
// 如果报错 "... because it has no configured signatory"，
// 那么你需要在 <userHome>/.gradle/gradle.properties 中，添加以下内容
// ```
// signing.keyId=....
// signing.password=....
// signing.secretKeyRingFile=....
// ```
// 如果 <userHome>/.gradle/gradle.properties 文件不存在，则新建一个。
signing {
    sign(publishing.publications)
}



/* ======================================================= */
/* Task Methods                                            */
/* ======================================================= */

/**
 * 注册 sourcesJar 任务，否则会没有 sources.jar 文件.
 */
tasks.register('sourcesJar', Jar) {
    dependsOn classes

    // gradle 8.0 以下用 classifier
    // classifier = 'sources'

    // gradle 8.0 及以上用 archiveClassifier
    archiveClassifier = 'sources'

    from sourceSets.main.allSource
}

/**
 * 注册 javadocJar 任务，否则会没有 javaDoc.jar 文件.
 */
tasks.register('javadocJar', Jar) {
    dependsOn javadoc

    // gradle 8.0 以下用 classifier
    // classifier = 'javadoc'

    // gradle 8.0 及以上用 archiveClassifier
    archiveClassifier = 'javadoc'

    from javadoc.destinationDir
}

 // 解决javadoc打包乱码
javadoc {
    options {
        encoding "UTF-8"
        charSet 'UTF-8'
        author true
        version true
        title "Hipoom Java Doc"
    }
}

publishing {
    // 定义目标仓库
    repositories {
        maven {
            url getLocalRepositoryDirectory(rootProject)
        }
    }

    // 定义产物
    publications { PublicationContainer publicationContainer ->
        hipoomLocal(MavenPublication) {
            // 基本信息
            groupId 'com.hipoom'
            artifactId getProductArtifactId()
            version getProductVersion()

            //打包源码和class
            from components.java

            // 增加 sources.jar 和 javadocs.jar
            artifact sourcesJar
            artifact javadocJar

            // 追加其他信息
            pom {
                name = "holder"
                description = "some functions for java."
                url = "sorry, no url provided."

                licenses {
                    license {
                        name = "The Apache License, Version 2.0"
                        url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                    }
                    developers {
                        developer {
                            id = "hipoom"
                            name = "hipoom"
                            email = "no email provided"
                        }
                        scm {
                            connection = "no-connection"
                            developerConnection = "no-developerConnection"
                            url = "no-url"
                        }
                    }
                }
            }
        }
    }
}


/* ======================================================= */
/* Private Methods                                         */
/* ======================================================= */

/**
 * 获取 local.properties 文件.
 * @return File
 */
static File getLocalProperties(Project rootProject) {
    File localProperties = rootProject.file("local.properties")
    if (!localProperties.exists()) {
        throw new IllegalStateException("[ERROR] 找不到 local.properties 文件.")
    }
    return localProperties
}

/**
 * 从 local.properties 中读取 key 对应的值。
 */
static String readLocalProperty(Project rootProject, String key) {
    File file = getLocalProperties(rootProject)
    InputStream is = file.newDataInputStream()
    Properties properties = new Properties()
    properties.load(is)

    Set<String> keys = properties.stringPropertyNames()
    if (!keys.contains(key)) {
        return null
    }

    return properties.getProperty(key)
}

/**
 * 返回仓库的本地路径。
 */
static String getLocalRepositoryDirectory(Project rootProject) {
    String dir = readLocalProperty(rootProject, "maven.repository.publish")
    if (dir == null || dir.isEmpty()) {
        throw new IllegalStateException("请在 local.properties 中设置 maven.repository.publish 用作本地的发布仓库地址。")
    }
    return dir
}